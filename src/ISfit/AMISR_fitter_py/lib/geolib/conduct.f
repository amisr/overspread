C     $Id: conduct.f,v 1.3 2009/07/08 13:37:12 brideout Exp $
C     Code originally written by Shunrong Zhang/Phil Erickson
C     Oct 25, 2006
C
       SUBROUTINE CONDUCT(NE,NI,N,TE,TI,TN,B,XH,XP)
       DOUBLE PRECISION NE,NI(2),NII(3),N(3),TE,TI,TN,B,XH,XP
       DOUBLE PRECISION E,EI(3),MI(3),M
       DOUBLE PRECISION TR,NUI(3),NUE
       DOUBLE PRECISION OE,XE,OE2NUE, OI,XI,OI2NUI
       DOUBLE PRECISION N_CM(3)
C-------------------------------------------------------------------------
C
C INPUTS
C    NE: ELECTRON DENSITY, M3
C    NI: ARRAY OF ION DENSITIES;
C        NI(1) = [O+];  NI(2) = [M+] MOLECULAR IONS, M^-3
C    N:  ARRAY OF NEUTRAL DENSITIES;
C        N(1) = [O]; N(2) = [N2]; N(3) = [O2], M^-3
C    TE: ELECTRON TEMPERATURE, K
C    TI: ION TEMPERATURE, K
C    TN: NEUTRAL TEMPERATURE, K
C    B: MAGNETIC FIELD STRENGTH, TESLA
C OUTPUTS
C    XH: HALL CONDUCTIVITY, (OHM M )^(-1)
C    XP: PEDERSEN CONDUCTIVITY (OHM M)^(-1)
C REFERENCES
C    R. W. SCHUNK AND A F. NAGY,
C     IONOSPHERES: PHYSICS, PLASMA PHYSICS, AND CHEMISTRY
C     CAMBRIDGE UNIVERSITY PRESS, CAMBRIDGE, UK, 2000.
C      SEE P131 FOR CONDUCTIVITY FORMULA;
C      SEE P97 AND P99 FOR COLLISION FREQUENCIES
C ASSUMPTIONS
C       IONS ARE O+ AND MOLECULAR IONS M+ WITH AVERAGE MASS 31
C       COLLISION FREQUENCY OF M+-NEUTRALS CALCULATED ASSUMING
C         50% NO+-NEUTRALS AND 50% O2+-NEUTRALS
C       O+-O COLLISION FREQUENCY INCREASED BY 1.3 OVER STANDARD BANKS
C       VALUE BANKS BASED ON RECENT STUDIES - CF.
C
C     A. C. BENNETT AND K. OMIDVAR, ALTERNATIVE METHOD FOR THE
C     THERMOSPHERIC ATOMIC OXYGEN DENSITY DETERMINATION, ADVANCES IN
C     SPACE RESEARCH, VOLUME 27, ISSUE 10, , 2001, PAGES 1685-1690.
C
C  WRITTEN BY SHUNRONG ZHANG, 2003
C  MODIFIED BY PHIL ERICKSON 2006-10-24 (CHANGED MOLECULAR
C             ION ASSUMPTION TO 2-ION APPROACH, ADDED
C             BURNSIDE FACTOR TO O+-O COLLISION FREQ)
C
C-------------------------------------------------------------------------
C   CONSTANTS - SOURCE VALUES: NIST
C     AVOGAD = AVOGADRO'S NUMBER
C     E = ELEMENTARY CHARGE, COULOMBS
C     EI(3) ARE CHARGES FOR O+, NO+, O2+ IN COULOMBS
C     MI(3) ARE MASSES FOR O+, NO+, O2+ IN KG
C     M = ELECTRON MASS IN KG
C
        AVOGAD = 6.0221415D23
        E = 1.60217653D-19
        M = 9.1093826D-31
        EI(1)  = E
        EI(2)  = E
        EI(3)  = E
        MI(1) =  16D-3 / AVOGAD
        MI(2) =  30D-3 / AVOGAD
        MI(3) =  32D-3 / AVOGAD
C
C    APPLY 50% NO+ AND 50% O2+ ASSUMPTION FOR MOLECULARS
C    NII(3) = ION DENSITIES FOR O+, NO+, O2+
C
        NII(1) = NI(1)
        NII(2) = 0.5 * NI(2)
        NII(3) = 0.5 * NI(2)
C
C    MSIS parameters need to be converted to cm^-3
C    Modified by Bill Rideout to avoid modifying input parms
C
        N_CM(1) = N(1) / 1.0E6
	    N_CM(2) = N(2) / 1.0E6
	    N_CM(3) = N(3) / 1.0E6
C
C COLLISION FREQUENCIES
C
CC O+ VS O (RESONANT),N2 AND O2 (NONRESONANT)
CC  NOTE: O+/O INCREASED BY BURNSIDE FACTOR = 1.3
        TR = (TI + TN)/2.D0
        NUI(1) = 1.3*(3.67D-11*N_CM(1)*SQRT(TR)*(1-0.064*LOG10(TR))**2)
     *         + 6.82D-10*N_CM(2)
     *         + 6.64D-10*N_CM(3)
CC NO+ VS O, N2, AND O2 (NONRESONANT)
        NUI(2) = 2.44D-10*N_CM(1)
     *         + 4.34D-10*N_CM(2)
     *         + 4.27D-10*N_CM(3)
CC O2+ VS O, N2 (NONRESONANT) AND O2 (RESONANT)
        NUI(3) = 2.31D-10*N_CM(1)
     *         + 4.13D-10*N_CM(2)
     *         + 2.59D-11*N_CM(3)*SQRT(TR) * (1-0.073*LOG10(TR))**2
CC ELECTRONS VS O, N2 AND O2
        NUE    = 8.9D-11*N_CM(1)*(1+5.7D-4*TE)*SQRT(TE)
     *         + 2.33D-11*N_CM(2)*(1-1.21D-4*TE)*TE
     *         + 1.82D-10*N_CM(3)*(1+3.6D-2*SQRT(TE))*SQRT(TE)
CC
CC FINAL CONDUCTIVITY CALCULATION
CC
        XH = 0
        XP = 0
        OE = (E/M)*B
        XE = (NE/NUE) * (E/M) * E
        OE2NUE = OE/NUE
        DO 1 I=1,3
           XI = (NII(I)/NUI(I)) * (EI(I) / MI(I)) * EI(I)
           OI = ( EI(I)/MI(I) )*B
           OI2NUI = OI/NUI(I)
           XP = XP + XI  / (1 + OI2NUI**2 )
           XH = XH + XI /( OI2NUI + 1/OI2NUI )
 1      CONTINUE
        XP = XP  + XE /(1 + OE2NUE**2)
        XH = - XH + XE /(OE2NUE + 1/OE2NUE)
        RETURN
        END
